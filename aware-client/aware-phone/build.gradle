apply plugin: 'com.android.application'

android {
    compileSdkVersion compile_sdk
    buildToolsVersion build_tools

    defaultConfig {
        applicationId "com.aware.phone"
        versionCode version_code
        versionName version_readable
        targetSdkVersion target_sdk
        minSdkVersion minimum_sdk

        //AWARE core sensors SyncAdapters
        resValue "string", "provider_accelerometer", android.defaultConfig.applicationId + ".provider.accelerometer"
        resValue "string", "provider_applications", android.defaultConfig.applicationId + ".provider.applications"
        resValue "string", "provider_aware", android.defaultConfig.applicationId + ".provider.aware"
        resValue "string", "provider_barometer", android.defaultConfig.applicationId + ".provider.barometer"
        resValue "string", "provider_battery", android.defaultConfig.applicationId + ".provider.battery"
        resValue "string", "provider_bluetooth", android.defaultConfig.applicationId + ".provider.bluetooth"
        resValue "string", "provider_communication", android.defaultConfig.applicationId + ".provider.communication"
        resValue "string", "provider_esm", android.defaultConfig.applicationId + ".provider.esm"
        resValue "string", "provider_gravity", android.defaultConfig.applicationId + ".provider.gravity"
        resValue "string", "provider_gyroscope", android.defaultConfig.applicationId + ".provider.gyroscope"
        resValue "string", "provider_installations", android.defaultConfig.applicationId + ".provider.installations"
        resValue "string", "provider_keyboard", android.defaultConfig.applicationId + ".provider.keyboard"
        resValue "string", "provider_light", android.defaultConfig.applicationId + ".provider.light"
        resValue "string", "provider_linear_accelerometer", android.defaultConfig.applicationId + ".provider.accelerometer.linear"
        resValue "string", "provider_locations", android.defaultConfig.applicationId + ".provider.locations"
        resValue "string", "provider_magnetometer", android.defaultConfig.applicationId + ".provider.magnetometer"
        resValue "string", "provider_network", android.defaultConfig.applicationId + ".provider.network"
        resValue "string", "provider_processor", android.defaultConfig.applicationId + ".provider.processor"
        resValue "string", "provider_proximity", android.defaultConfig.applicationId + ".provider.proximity"
        resValue "string", "provider_rotation", android.defaultConfig.applicationId + ".provider.rotation"
        resValue "string", "provider_screen", android.defaultConfig.applicationId + ".provider.screen"
        resValue "string", "provider_significant", android.defaultConfig.applicationId + ".provider.significant"
        resValue "string", "provider_telephony", android.defaultConfig.applicationId + ".provider.telephony"
        resValue "string", "provider_temperature", android.defaultConfig.applicationId + ".provider.temperature"
        resValue "string", "provider_timezones", android.defaultConfig.applicationId + ".provider.timezone"
        resValue "string", "provider_traffic", android.defaultConfig.applicationId + ".provider.traffic"
        resValue "string", "provider_wifi", android.defaultConfig.applicationId + ".provider.wifi"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

if (System.getenv("storeFile") != null && System.getenv("storePassword") != null && System.getenv("keyAlias") != null && System.getenv("keyPassword") != null) {
    android.signingConfigs.release.storeFile = file(System.getenv("storeFile"))
    android.signingConfigs.release.storePassword = System.getenv("storePassword")
    android.signingConfigs.release.keyAlias = System.getenv("keyAlias")
    android.signingConfigs.release.keyPassword = System.getenv("keyPassword")
} else {
    println("No keystore defined. Unsigned!")
    android.buildTypes.release.signingConfig = null
}

repositories {
    maven { url "https://jitpack.io" } //MPAndroidChart, AWARE
}

dependencies {
    implementation "com.android.support:cardview-v7:$support_libs"
    implementation "com.android.support:design:$support_libs"
    implementation('me.dm7.barcodescanner:zbar:1.8.4') {
        exclude group: 'com.android.support', module: "support-v4"
    }
    api project(':aware-core')
}

